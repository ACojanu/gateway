#
# Copyright (c) 2007-2014 Kaazing Corporation. All rights reserved.
# 
# Licensed to the Apache Software Foundation (ASF) under one
# or more contributor license agreements.  See the NOTICE file
# distributed with this work for additional information
# regarding copyright ownership.  The ASF licenses this file
# to you under the Apache License, Version 2.0 (the
# "License"); you may not use this file except in compliance
# with the License.  You may obtain a copy of the License at
# 
#   http://www.apache.org/licenses/LICENSE-2.0
# 
# Unless required by applicable law or agreed to in writing,
# software distributed under the License is distributed on an
# "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
# KIND, either express or implied.  See the License for the
# specific language governing permissions and limitations
# under the License.
#

# tcp client sends and receives one byte (to avoid fragmentation!)

connect tcp://localhost:8010
connected

read notify CONNECTED

write "a"

#read "a"

write await DOWNSTREAM_COMPLETED 

close
closed


# wseb create request from proxy service wse connect

accept http://localhost:8011/wse_backend/;e/cb
accepted
connected

read method "GET"
read version "HTTP/1.1"
# read header "X-WebSocket-Version" "wseb-1.1"
read header "X-Sequence-No" /(?<sequence>\b[0-9]{1,3}\b)/
read closed

write status "201" "Created" 
write version "HTTP/1.1"
write header "X-WebSocket-Version" "wseb-1.1"
write header "Content-Type" "text/plain;charset=utf-8"
write header content-length
write "http://localhost:8011/wse_backend/;e/ub/QD12TQhXT6I5KeCrDe6g0IC9qoDkIioh\n"
write "http://localhost:8011/wse_backend/;e/db/QD12TQhXT6I5KeCrDe6g0IC9qoDkIioh\n"
write notify CREATED
write close

# wseb downstream request from proxy service wse connect


accept http://localhost:8011//wse_backend/;e/db/QD12TQhXT6I5KeCrDe6g0IC9qoDkIioh
accepted
connected

read await CREATED
read method "GET"
read version "HTTP/1.1"
read header "X-Sequence-No" 2
read closed

write status "200" "OK"
write version "HTTP/1.1"
write header "Content-Type" "application/octet-stream"
write header "Connection" "close"
write flush

write [0x01 0x30 0x30 0xff] # NO-OP
write flush

write await DATA_RECEIVED
# binary frame one byte long
write [0x80 0x01]
write "a"

# close command
write [0x01 0x30 0x32 0xff]
# reconnect command
write [0x01 0x30 0x31 0xff]

write notify DOWNSTREAM_COMPLETED


# wseb upstream requests from proxy service wse connect

accept http://localhost:8011//wse_backend/;e/ub/QD12TQhXT6I5KeCrDe6g0IC9qoDkIioh
accepted
connected

read await CREATED

read method "POST"
read version "HTTP/1.1"
read header "Content-Type" "application/octet-stream"
read header "X-Sequence-No" 3

# Data frame
read [0x80 0x01]
read "a"
read notify DATA_RECEIVED

# reconnect
read [0x01 0x30 0x31 0xFF]

read closed

write status "200" "OK"
write version "HTTP/1.1"
write header content-length
write close

accepted
connected

read await CREATED

read method "POST"
read version "HTTP/1.1"
read header "Content-Type" "application/octet-stream"
read header "X-Sequence-No" 3

# close
read [0x01 0x30 0x32 0xFF]

# reconnect
read [0x01 0x30 0x31 0xFF]

read closed

write status "200" "OK"
write version "HTTP/1.1"
write header content-length
write close
